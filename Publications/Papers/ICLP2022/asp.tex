\section{Defeasible Reasoning with Answer Set Programming}\label{sec:defeasible_asp}



\subsection{Introduction}
The purpose of this section is to give an account of the work we have been doing using Answer Set Programming (ASP) to formalize and reason about legal rules. This approach is complementary to the one described before using SMT solvers. Our intention is to present how some core legal reasoning tasks can be implemented in ASP while keeping the ASP representation readable and intuitive and respecting the idea of having an `isomorphism' between the rules and the encoding. Please see the appendix for a brief overview of ASP and references for further reading.

Our work in this section is inspired by \cite{DBLP:conf/iclp/WanGKFL09} and we borrow some of their notation/terminology. Readers will note that there are similarities between the use of predicates such as $according\_to$, $defeated$, $opposes$ in our ASP encoding, to reason about rules interacting with each other, and similar predicates that the authors of \cite{DBLP:conf/iclp/WanGKFL09} use in their work. However our ASP implementation is much more specific to legal reasoning whereas they seek to implement very general logic based reasoning mechanisms. We independently developed our `meta theory' for how rule modifiers interact with the rules and with each other and there are further original contributions like a proposed axiom system for what we call `legal models'. An interesting avenue of future work could be to compare our approaches within the framework of legal reasoning.

The work in this section builds on the work in \cite{morris21:_const_answer_set_progr_tool} and hence uses many of the same predicates/notation and terminology. 
% The author of \cite{morris21:_const_answer_set_progr_tool} was a member of the same research group as the authors of this paper at SMU in 2020--2021.


\subsection{Formal Setup}
Let the tuple $Config = (R,F,M,I)$ denote a $configuration$ of legal rules. The set $R$ denotes a set of rules of the form $pre\_con(r)\rightarrow concl(r)$. These are `naive' rules with no information pertaining to any of the other rules in $R$. $F$ is a set of positive atoms that describe facts of the legal scenario we wish to consider. $M$ is a set of the binary predicates $despite$, $subject\_to$ and $strong\_subject\_to$. $I$ is a collection of minimal inconsistent sets of positive atoms. Henceforth for a rule $r$, we may write $C_{r}$ for its conclusion $Concl(r)$.

Note that, throughout this section, given any rule $r$, $C_{r}$ is assumed to be a single positive atom. That is, there are no disjunctions or conjunctions in rule conclusions. Also any rule pre-condition ($pre\_con(r)$) is assumed to be a conjunction of positive and negated atoms. Here negation denotes `negation as failure'.  

Throughout this document, whenever we use an uppercase or lowercase letter (like $r$, $r_{1}$, $R$ etc.) to denote a rule that is an argument, in a binary predicate, we mean the unique integer rule \textsc{id} associated with that rule. The binary predicate $legally\_valid(r,c)$ intuitively means that the rule $r$ is `in force' and it has conclusion $c$. Here $r$ typically is an integer referring to the rule \textsc{id} and $c$ is the atomic conclusion of the rule. The unary predicate $is\_legal(c)$ intuitively means that the atom $c$ legally holds/has legal status. The predicates $despite$, $subject\_to$ and $strong\_subject\_to$ all cause some rules to override others. Their precise properties will be given next.


\subsection{Semantics}

A set $S$ of $is\_legal$ and $legally\_valid$ predicates is called a \textit{legal model} of $Config = (R,F,M,I)$, if and only if
\begin{description}
\item[(A1)]$\forall f \in F$ $is\_legal(f) \in S$.

\item[(A2)] $\forall r \in R$, if $legally\_valid(r,C_{r}) \in S$. then $S\models is\_legal(pre\_con(r))$ and $S\models is\_legal(C_{r})$ \footnote{By $S\models is\_legal(pre\_con(r))$ we mean that for each positive atom $b_{i}$ in the conjunction, $is\_legal(b_{i}) \in S$ and for each negation-as-failure body atom $not$ $b_{j}$ in the conjunction $is\_legal(b_{j})\notin S$ }

\item[(A3)] $\forall c$, if $is\_legal(c) \in S$, then either $c\in F$ or there exists $r \in R$ such that $legally\_valid(r,C_{r}) \in S$ and $c= C_{r}$.

\item[(A4)] $\forall r_{i}, r_{j} \in R$, if $despite(r_{i}, r_{j}) \in M$ and $S\models is\_legal(pre\_con(r_{j}))$, then $legally\_valid(r_{i},C_{r_{i}}) \notin S$

\item[(A5)] $\forall r_{i}, r_{j} \in R$, if $strong\_subject\_to(r_{i}, r_{j}) \in M$ and $legally\_valid(r_{i},C_{r_{i}}) \in S$, then $legally\_valid(r_{j},C_{r_{j}}) \notin S$


\item[(A6)] $\forall r_{i},r_{j} \in R$ if $subject\_to(r_{i},r_{j}) \in M$, and $legally\_valid(r_{i},C_{r_{i}}) \in S$ and there exists a minimal conflicting set $k \in I$ such that $C_{r_{i}} \in k$ and $C_{r_{j}}\in k$ and $is\_legal(k\setminus \{C_{r_{j}})\})\subseteq S $, then $legally\_valid(r_{j},C_{r_{j}}) \notin S$. Note than in our system, any minimal inconsistent set must contain at least 2 atoms. \footnote{For a set of atoms $A$, by $is\_legal(A)$, we mean the set $\{is\_legal(a)\mid a\in A\}$} 

\item[(A7)] $\forall r\in R$, if $S\models pre\_con(r)$, but $legally\_valid(r,C_{r})\notin S$, then it must be the case that at least one of A4 or A5 or A6 has caused the exclusion of $legally\_valid(r,C_{r})$. That is if $S\models pre\_con(r)$, then unless this would violate one of A5, A6 or A7, it must be the case that $legally\_valid(r,C_{r})\in S$.
\end{description}

\subsection{Some remarks on axioms A1--A7}
We now give some informal intuition behind some of the axioms and their intended effects.

A1 says that all facts in $F$ automatically gain legal status, that is, they legally hold. The set $F$ represents indisputable facts about the legal scenario we are considering.

A2 says that if a rule is `in force' then it must be the case that both the pre-condition and conclusion of the rule have legal status. Note that it is not enough if simply require that the conclusion has legal status as more than one rule may enforce the same conclusion or the conclusion may be a fact, so we want to know exactly which rules are in force as well as their conclusions.

A3 says that anything that has legal status must either be a fact or be a conclusion of some rule that is in force.

A4--A6 describe the semantics of the three modifiers. The intuition for the three modifiers will be discussed next. Firstly, it may help the reader to read the modifiers in certain ways. $despite(r_{i},r_{j})$ should be read as `despite $r_{i}$, $r_{j}$'. Thus $r_{i}$ here is the `subordinate rule' and $r_{j}$ is the `dominating' rule. The idea here is that once the precondition of the dominating rule $r_{j}$ is true, it invalidates the subordinate rule $r_{i}$ regardless of whether the dominating rule itself is then invalidated by some other rule. For \textit{strong subject to}, the intended reading for $strong\_subject\_to(r_{i},r_{j})$ is something like `(strong) subject to $r_{i}$, $r_{j}$'. Here $r_{i}$ can be considered the dominating rule and $r_{j}$ the subordinate. Once the dominating rule is in force, then it invalidates the subordinate rule. The intended reading for $subject\_to(r_{i},r_{j})$ is `subject to $r_{i}$, $r_{j}$'. For the subordinate rule $r_{j}$ to be invalidated, it has to be the case that the dominating rule $r_{i}$ is in force and there is a minimal inconsistent set $k$ in $I$ that contains the two atoms in the conclusions of the two rules and, $is\_legal(k\setminus \{C_{r_{j}})\})\subseteq S $. These minimal inconsistent sets along with the \textit{subject to} modifier give us a way to incorporate a classical-negation-like effect into our system. We are able to say which things contradict each other. Note that in our system, if say $\{a,b\}$ is a minimal inconsistent set, then it is possible for both $is\_legal(a)$ and $is\_legal(b)$ to be in a single legal model, if they are both facts or they are conclusions of rules that have no modifiers linking them. These minimal inconsistent sets only play a role where a $subject\_to$ modifier is involved. The reason for doing this is that this offers greater flexibility rather than treating $a$ and $b$ as pure logical negatives of each other that cannot be simultaneously true in a legal model. We will give examples later on to illustrate these modifiers.

A7 says essentially that A4--A6 represent the only ways in which a rule whose pre-condition is true may nevertheless be invalidated, and any rule whose precondition is satisfied and is not invalidated directly by some instance of A4--A6, must be in force. 

Note that there maybe legal rule configurations for which no legal models exist. See the appendix for a discussion of some 'pathological' rule configurations.

\subsection{ASP encoding}
Here is an ASP encoding scheme for a configuration $Config = (R,F,M,I)$ of legal rules.
\begin{lstlisting}[language=Prolog, numbers=left]
% For any f in F, we have:
is_legal(f). 
% All the modifiers get added as facts like for example:
despite(1,2).
% Any rule r in R is encoded using the general schema:
according_to(r,C_r):-is_legal(pre_con(r)).
% Given a minimal inconsistent set {a_1,a_2,...,a_n}, this corresponds to a set of rules:
opposes(a_1,a_2):-is_legal(a_2),is_legal(a_3),...,is_legal(a_n).
opposes(a_1,a_3):-is_legal(a_2),is_legal(a_4)...,is_legal(a_n).  % etc ...
opposes(a_n-1,a_n):-is_legal(a_1),...,is_legal(a_n-2).               
% Opposes is a symmetric relation
opposes(X,Y):-opposes(Y,X).
% Encoding for 'despite'
defeated(R,C,R1) :-
    according_to(R,C), according_to(R1,C1), despite(R,R1).
%Encoding for 'subject_to'
defeated(R,C,R1) :-
    according_to(R,C), legally_valid(R1,C1),
    opposes(C,C1), subject_to(R1,R).
% Encoding for 'strong_subject_to'
defeated(R,C,R1) :-
    according_to(R,C), legally_valid(R1,C1),
    strong_subject_to(R1,R).

not_legally_valid(R) :- defeated(R,C,R1).
legally_valid(R,C):-according_to(R,C),not not_legally_valid(R).
is_legal(C):-legally_valid(R,C).
\end{lstlisting}
\subsection{Proposition}

\begin{proposition}\label{lemma:legal_model_of_config}
For a configuration $Config=(R,F,M,I)$, let the above encoding be the program $ASP_{Config}$. Then given an answer set $A_{Config}$ of $ASP_{Config}$ let $S_{A_{Config}}$ be the set of $is\_legal$ and $legally\_valid$ predicates in $A_{Config}$. Then $S_{A_{Config}}$ is a legal model of $Config$. 
\end{proposition}


$Proof$ See Appendix of full paper. $\square$


\subsection{Example}
Let us see how the running example would work in the ASP setting. We have 3 rules encoded as below, there are no minimal inconsistent sets. There are 3 modifiers: $despite(2,3)$,                           $strong\_subject\_to(1,3)$, $strong\_subject\_to(1,2)$
\begin{lstlisting}[language=Prolog, numbers=left]
despite(2,3).
strong_subject_to(1,3).
strong_subject_to(1,2).
according_to(1,max_spd(v,d,r,90)):-is_legal(is_workday(d)),
is_legal(is_car(v)).
according_to(2,max_spd(v,d,r,130)):-is_legal(is_highway(r)),
   is_legal(is_car(v)).
according_to(3,max_spd(v,d,r,320)):-is_legal(is_highway(r)),
   is_legal(is_sports_car(v)).

% Encoding for 'despite'
defeated(R,C,R1) :-
    according_to(R,C), according_to(R1,C1), despite(R,R1).
%Encoding for 'subject_to'
defeated(R,C,R1) :-
    according_to(R,C), legally_valid(R1,C1),
    opposes(C,C1), subject_to(R1,R).
% Encoding for 'strong_subject_to'
defeated(R,C,R1) :-
    according_to(R,C), legally_valid(R1,C1),
    strong_subject_to(R1,R).

not_legally_valid(R) :- defeated(R,C,R1).
legally_valid(R,C):-according_to(R,C),not not_legally_valid(R).
is_legal(C):-legally_valid(R,C).
\end{lstlisting}

When the initial set of facts $F$ is the set
\begin{lstlisting}[language=Prolog, numbers=left]
is_legal(is_workday(d)).
is_legal(is_car(v)).
is_legal(is_highway(r)).
is_legal(is_sports_car(v)).
\end{lstlisting}
we get exactly one legal max speed given by 
\begin{lstlisting}[language=Prolog, numbers=left]
is_legal(max_spd(v,d,r,90)).
\end{lstlisting}
One can check this by adding the rule 
\begin{lstlisting}[language=Prolog, numbers=left]
legal_max_spd(X):- is_legal(max_spd(v,d,r,X)). 
\end{lstlisting}
and running the s(CASP) query $?- legal\_max\_spd(X).$, which returns the binding $X = 90$. This is the unique legal maximum speed which can be seen via use of the rule 
\begin{lstlisting}[language=Prolog, numbers=left]
legal_max_spd(X):- X > 90, is_legal(max_spd(v,d,r,X)). 
\end{lstlisting}
Now running the query as above we see that there is no solution. 
When removing
$is\_legal(is\_workday(d))$ from $F$, we get exactly one legal max speed of $320$, and when  $is\_legal(is\_sports\_car(v))$, and $is\_legal(is\_workday(d))$ are both removed from $F$ we get exactly one exactly legal max speed of $130$.

% We note here that if all 4 facts as in the first initial fact set are passed
% to the SMT solver following the approach presented in
% \secref{sec:defeasible_classical} then the SMT solver will also return exactly
% one max speed of $90$. If instead we choose to modify the initial fact set by
% negating $is\_work\_day(d)$, then the SMT solver will return exactly one max
% speed of $320$ and if modify the initial fact set by negating both
% $is\_work\_day(d)$ and $is\_sports\_car(v)$, we get exactly one max speed of
% $130$.







%%% Local Variables:
%%% mode: latex
%%% TeX-master: "main"
%%% End:
